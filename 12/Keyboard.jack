// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int kbrAddr; // addr of keyboard memory map

    /** Initializes the keyboard. */
    function void init() {
        let kbrAddr = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(kbrAddr);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        
        // wait for key press
        while (Keyboard.keyPressed() = 0) { }
        let c = Keyboard.keyPressed();

        // wait until key is released
        while (~(Keyboard.keyPressed() = 0)) { }
        
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     * 
     * 100 character input limit.
     */
    function String readLine(String message) {
        var char c;
        var String res;
        let res = String.new(100);
        
        do Output.printString(message);

        while (true) {
            let c = Keyboard.readChar();

            if (c = String.newLine()) { 
                do Output.println();
                return res;
            } else {
                if (c = String.backSpace()) {
                    // do Output.backSpace();
                    do res.eraseLastChar(); // huh...this suffices. makes no sense.
                } else {
                    do res.appendChar(c);
                }
            }
        }
        return res; // unreachable
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
